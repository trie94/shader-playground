Shader "Unlit/LinearGradient"
{
    Properties
    {
        _Color1 ("Color 1", Color) = (1, 1, 1, 0)
        _Color2 ("Color 2", Color) = (1, 1, 1, 0)
        _UpVector ("Up Vector", Vector) = (0, 1, 0, 0)
		_Intensity ("Intensity", Float) = 1.0
        _Exponent ("Exponent", Float) = 1.0
    }
    SubShader
    {
        Tags { "RenderType"="Background" "Queue"="Background" "PreviewType"="Skybox" }
        LOD 100
		ZWrite Off Cull Off Fog { Mode Off }

        Pass
        {
            CGPROGRAM
            #pragma vertex vert
            #pragma fragment frag

            #include "UnityCG.cginc"

            struct appdata
            {
                float4 vertex : POSITION;
                float3 texcoord : TEXCOORD0;
            };

            struct v2f
            {
                float3 texcoord : TEXCOORD0;
                float4 vertex : SV_POSITION;
            };

			fixed4 _Color1;
			fixed4 _Color2;
			half4 _UpVector;
			half _Intensity;
			half _Exponent;

            v2f vert (appdata v)
            {
                v2f o;
                o.vertex = UnityObjectToClipPos(v.vertex);
                o.texcoord = v.texcoord;
                return o;
            }

            fixed4 frag (v2f i) : SV_Target
            {
                half d = dot(normalize (i.texcoord), _UpVector) * 0.5 + 0.5;
				return lerp (_Color1, _Color2, pow (d, _Exponent)) * _Intensity;
            }
            ENDCG
        }
    }
}
